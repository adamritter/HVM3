// Multiplication by squared addition with optimal evaluation

// Repeated Application
@rep(n f x) = ~ n !f !x {
  0: x
  p: !&0{f0 f1}=f (f0 @rep(p f1 x))
}

// Squared Application
@sqr(n f x) = ~ n !f !x {
  0: x
  p:
    !&0{p0 p1}=(+ p 1)
    !&0{fA f0}=f
    !&0{f1 f2}=fA
    @sqr((/ p0 2) λk(f0 (f1 k)) @rep((% p1 2) f2 x))
}

// Bits (Scott)
@o(x) = λo λi λe (o x)
@i(x) = λo λi λe (i x)
@e    = λo λi λe e

// Creates an all-zero Bits
@zero(s) = ~s{
  0: @e
  p: @o(@zero(p))
}

// U32 -> Bits
@bits(n) = ~ n {
  0: @e
  p: !&0{n0 n1}=(+ p 1) ~ (% n0 2) !n1 {
    0: @o(@bits((/ n1 2)))
    1: @i(@bits((/ n1 2)))
    p: *
  }
}

// Bits -> U32
@u32(xs) = (xs
  λp0 (+ (* 2 @u32(p0)) 0)
  λp1 (+ (* 2 @u32(p1)) 1)
  0)

// Bits increment
@inc(x) = λo λi λe (x i λop(o @inc(op)) e)

// Addition with carry
@add = λa (a
  λap λb (b λbp λap @o((@add ap bp)) λbp λap @i(     (@add ap bp))  @e ap)
  λap λb (b λbp λap @i((@add ap bp)) λbp λap @o(@inc((@add ap bp))) @e ap)
  λb b)

// Multiplication by squared addition
@mul(a b) = @u32(@sqr(a (@add @bits(b)) @zero(64)))

@main = @mul(12345 12345)

//WORK: 233357193 interactions
//TIME: 3.202 seconds
//SIZE: 1402714952 nodes
//PERF: 72.890 MIPS
