// This is the HVM version of the naive λ-Calculus enumerator. It superposes all
// binary λ-calculus strings, parses, and applies to the equation we want to
// solve. Despite the use of superpositions, this performs about the same as the
// Haskell version, since HVM is forced to enumerate all terms anyway, and not a
// lot of sharing is possible. This takes about 32 million interactions. A
// better approach is provided in the lambda_enumerator_optimal.hvml file, which
// brings this number down to just 72k interactions.
// UPDATE: actually - by just avoiding the issue depicted on:
// https://gist.github.com/VictorTaelin/fb798a5bd182f8c57dd302380f69777a
// We can bring this naive BLC enumerator down to 1.7m interactions. Not quite
// as fast as 72k, but this makes it ~37x faster than the Haskell version.

data List {
  #Nil
  #Cons{head tail}
}

data Bits {
  #O{pred}
  #I{pred}
  #E
}

data Term {
  #Lam{bod}
  #App{fun arg}
  #Var{idx}
  #Sub{val}
}

data pair {
  #Pair{fst snd}
}

data Maybe {
  #None
  #Some{value}
}

// Prelude
// -------

@if(c t f) = ~ c {
  0: f
  p: t
}

@when(c t) = ~ c {
  0: *
  p: t
}

@tail(xs) = ~ xs {
  #Nil: *
  #Cons{h t}: t
}

// Parsing
// -------

@do_parse_term(src) =
  ! &0{src term} = @parse_term(src)
  @do_bind_term(term)

@parse_term(src) = ~src {
  #O{src}:
    ! &0{src nat} = @parse_nat(src)
    &0{src #Var{nat}}
  #I{src}: ~src {
    #O{src}:
      ! &0{src bod} = @parse_term(src)
      &0{src #Lam{bod}}
    #I{src}:
      ! &0{src fun} = @parse_term(src)
      ! &0{src arg} = @parse_term(src)
      &0{src #App{fun arg}}
    #E: * 
  }
  #E: *
}

@parse_nat(src) = ~src {
  #O{src}: &0{src 0}
  #I{src}:
    ! &0{src nat} = @parse_nat(src)
    &0{src (+ 1 nat)}
  #E: &0{#E 0}
}

// Binding
// -------

@do_bind_term(term) =
  ! &0{ctx term} = @bind_term(term #Nil)
  term

@bind_term(term ctx) = ~term !ctx {
  #Lam{bod}:
    ! &0{ctx bod} = @bind_term(bod #Cons{#Some{$x} ctx})
    &0{@tail(ctx) #Lam{λ$x bod}}
  #App{fun arg}:
    ! &0{ctx fun} = @bind_term(fun ctx)
    ! &0{ctx arg} = @bind_term(arg ctx)
    &0{ctx #App{fun arg}}
  #Var{idx}: @get(idx ctx)
  #Sub{val}: *
}

@get(idx ctx) = ~ idx !ctx {
  0: ~ ctx {
    #Nil: *
    #Cons{h t}: ~ h {
      #None: *
      #Some{x}: &0{#Cons{#None t} x}
    }
  }
  p: ~ ctx {
    #Nil: *
    #Cons{h t}:
      ! &0{t x} = @get(p t)
      &0{#Cons{h t} x}
  }
}

// Stringification
// ---------------

@show_nat(nat) = ~nat { 
  0: λk #Cons{'Z' k}
  p: λk #Cons{'S' (@show_nat(p) k)}
}

@show_dec(n r) =
  ! &10000002{n n0} = n
  ! &10000002{n n1} = n
  ! chr = (+ (% n 10) '0')
  ~ (< n0 10) !chr !r {
    0: @show_dec((/ n1 10) #Cons{chr r})
    t: #Cons{chr r}
  }

@do_show_dec(n) = @show_dec(n #Nil)

@show_bits(bits) = ~bits {
  #O{pred}: λk #Cons{'#' #Cons{'O' #Cons{'{' (@show_bits(pred) #Cons{'}' k})}}}
  #I{pred}: λk #Cons{'#' #Cons{'I' #Cons{'{' (@show_bits(pred) #Cons{'}' k})}}}
  #E: λk #Cons{'#' #Cons{'E' k}}
}

@do_show_bits(bits) = (@show_bits(bits) #Nil)

@show_term(term dep) = ~term !dep {
  #Var{idx}: λk
    @show_dec((- (- dep idx) 1) k)
  #Lam{bod}: λk
    !&0{d0 d1}=dep
    #Cons{'λ' (@show_term((bod #Var{d0}) (+ d1 1)) k)}
  #App{fun arg}: λk
    !&0{d0 d1}=dep
    #Cons{'(' (@show_term(fun d0)
    #Cons{' ' (@show_term(arg d1)
    #Cons{')' k})})}
  #Sub{val}: *
}

@do_show_term(term) = (@show_term(term 0) #Nil)

// Equality
// --------

@eq(a b dep) = ~ a !b !dep {
  #Lam{a_bod}: ~ b !dep {
    #Lam{b_bod}:
      !&1{dep d0}=dep
      !&1{dep d1}=dep
      !&1{dep d2}=dep
      @eq((a_bod #Var{d0}) (b_bod #Var{d1}) (+ 1 d2))
    #App{b_fun b_arg}: 0
    #Var{b_idx}: 0
    #Sub{b_val}: *
  }
  #App{a_fun a_arg}: ~ b !dep {
    #Lam{b_bod}: 0
    #App{b_fun b_arg}:
      !&1{dep d0}=dep
      !&1{dep d1}=dep
      (& @eq(a_fun b_fun d0) @eq(a_arg b_arg d1))
    #Var{b_idx}: 0
    #Sub{b_val}: *
  }
  #Var{a_idx}: ~ b !dep {
    #Lam{b_bod}: 0
    #App{b_fun b_arg}: 0
    #Var{b_idx}: (== a_idx b_idx)
    #Sub{b_val}: *
  }
  #Sub{a_val}: *
}

// Evaluation
// ----------

@wnf(term) = ~ term { 
  #Lam{bod}: #Lam{bod}
  #App{fun arg}: @app(@wnf(fun) arg)
  #Var{idx}: #Var{idx}
  #Sub{val}: #Sub{val}
}

@app(f x) = ~ f !x {
  #Lam{bod}: @wnf((bod @wnf(x)))
  #App{fun arg}: #App{#App{fun arg} x}
  #Var{idx}: #App{#Var{idx} x}
  #Sub{val}: #App{#Sub{val} x}
}

// Normalization
// -------------

@nf(term) = ~ @wnf(term) {
  #Lam{bod}: #Lam{λx @nf((bod #Sub{x}))}
  #App{fun arg}: #App{@nf(fun) @nf(arg)}
  #Var{idx}: #Var{idx}
  #Sub{val}: val
}

// Enumeration
// -----------

// Enums all Bins of given size (label 1)
@all1(s) = ~s{
  0: #E
  p: !&2{p0 p1}=p &2{
    #O{@all1(p0)}
    #I{@all1(p1)}
  }
}

// Tests
// -----

//A= λt (t ^1 ^2)
//A= λ((Z SZ) SSZ)
@A = #Lam{λt #App{#App{t #Var{1}} #Var{2}}}

//B= λt (t ^2 ^1)
//B= λ((Z SSZ) SZ)
@B = #Lam{λt #App{#App{t #Var{2}} #Var{1}}}

//R= λx (x λa λb λt (t b a))
//R= λ(Z λλλ((SSSZ SSZ) SZ))
@R = #Lam{λx #App{x #Lam{λa #Lam{λb #Lam{λt #App{#App{t b} a}}}}}}

@X = @all1(25)

// Solve `?x` in `λaλb(?x λt(t a b)) == λaλbλt(t b a)`
@main =
  ! &5{x0 x1} = @do_parse_term(@X)
  ! solved    = @eq(@nf(#App{x0 @A}) @B 0) // (?x A) == B
  @when(solved @do_show_term(x1))
